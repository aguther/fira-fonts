// --------------------------------------------------------------------------------------------------------------------
plugins {
    id 'nebula.ospackage' version '4.0.0'
    id "de.undercouch.download" version '3.1.1'
}

// --------------------------------------------------------------------------------------------------------------------
task downloadPackages(type: de.undercouch.gradle.tasks.download.Download) {
    src "https://github.com/mozilla/Fira/archive/${version}.zip"
    dest project.file("$buildDir/downloaded/${version}.zip")
    onlyIfNewer true
    acceptAnyCertificate true
}

// --------------------------------------------------------------------------------------------------------------------
def extractionDirectory = project.file("$buildDir/extracted")

task extractPackages(dependsOn: downloadPackages) {

    outputs.dir extractionDirectory

    doLast {
        copy {
            from zipTree(downloadPackages.dest)
            into extractionDirectory
        }
    }
}

// --------------------------------------------------------------------------------------------------------------------
def ttfDirectory = "$extractionDirectory/Fira-${version}/ttf"
def targetDirectory = '/usr/share/fonts/fira'

ospackage {
    vendor = 'The Mozilla Foundation and Telefonica S.A.'
    url = 'https://github.com/mozilla/Fira'
    license = 'SIL Open Font License, Version 1.1'

    summary = 'Mozilla\'s Fira Type Family'
    description = 'Mozilla\'s Fira Type Family'

    packageGroup = 'User Interface/X'

    packager = 'Andreas Guther'

    version = version
    release = rpmRelease
    arch = NOARCH
    os = LINUX

    user = 'root'

    into "$targetDirectory"

    from("$ttfDirectory") {
        into ''
    }
}

buildRpm {
    dependsOn extractPackages
}

// --------------------------------------------------------------------------------------------------------------------
defaultTasks 'clean', 'buildRpm'
